buildscript {
    repositories {
        mavenCentral()
        maven { url = "https://jcenter.bintray.com/" }
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        gradlePluginPortal()
    }
    dependencies {
        classpath ('com.anatawa12.forge:ForgeGradle:1.2-1.0.+') {
            changing = true
        }
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.4.0"
    }
}

apply plugin: 'forge'
apply plugin: 'com.matthewprenger.cursegradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.

archivesBaseName = 'telegrambridge'
group 'ru.glitchless.telegrambridge'
version "1.7.10-${project.mod_version}"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

minecraft {
    version = "1.7.10-10.13.4.1614-1.7.10"
    replaceIn "TelegramBridgeMod.java"
    replace '${version}', project.mod_version
    runDir = "run"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.mod_version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.mod_version, 'mcversion': project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

configurations {
    embed
    compile.extendsFrom(embed)
}

dependencies {
    embed project(":core")
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "telegrambridge",
                "Specification-Vendor"    : "lionzxy",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${project.mod_version}",
                "Implementation-Vendor"   : "lionzxy",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
    from configurations.embed.collect {
        exclude '.classpath'
        exclude '.project'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'module-info.class'
        exclude 'META-INF/NOTICE'
        it.isDirectory() ? it : zipTree(it)
    }
}


artifacts {
    archives jar
}

curseforge {
    apiKey = "${CurseForgeApiToken}"
    project {
        id = '357271'
        changelog = "${last_changelog}"
        releaseType = "${mod_state}"
        addGameVersion '1.7.10'

        mainArtifact(jar) {
            displayName = "TelegramBridge $project.version"
        }
    }
}
